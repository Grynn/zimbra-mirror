#!/usr/bin/perl
# 
# ***** BEGIN LICENSE BLOCK *****
# Zimbra Collaboration Suite Server
# Copyright (C) 2005, 2006, 2007, 2008, 2009, 2010 Zimbra, Inc.
# 
# The contents of this file are subject to the Zimbra Public License
# Version 1.3 ("License"); you may not use this file except in
# compliance with the License.  You may obtain a copy of the License at
# http://www.zimbra.com/license.
# 
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied.
# ***** END LICENSE BLOCK *****
# 
use strict;
use lib qw(/opt/zimbra/zimbramon/lib /opt/zimbra/libexec/scripts);
use Net::DNS;
use Socket;
use Net::LDAP;
use Net::LDAP::LDIF;
use Getopt::Long;

my (%c,%loaded,%saved,$rc);
my ($oldServerName, $newServerName, $help, $verbose, $force, $delete, $skipusers, $usersonly);

my (undef, undef,$uid,$gid) = getpwnam('zimbra');
if ($> ne $uid) {
  print "Must be run as user zimbra.\n";
  &usage
}

GetOptions("help" => \$help, 
    "oldname=s" => \$oldServerName,
    "newname=s" => \$newServerName,
    "force" => \$force,
    "deletelogger" => \$delete,
    "skipusers" => \$skipusers,
    "usersonly" => \$usersonly,
    "verbose+" => \$verbose);

&usage if ($help or !$newServerName);



$c{zmlocalconfig}="/opt/zimbra/bin/zmlocalconfig";
$c{zimbra_home} = getLocalConfig("zimbra_home");
$c{zmprov}="$c{zimbra_home}/bin/zmprov -l --";
$c{zmcertmgr}="$c{zimbra_home}/bin/zmcertmgr";

if ($usersonly && !$oldServerName) {
  print "--usersonly requries --oldServerName.\n";
  exit 0;
}

if ($oldServerName) {
  $c{zimbra_server_hostname} = $oldServerName;
} else {
  $c{zimbra_server_hostname} = getLocalConfig("zimbra_server_hostname");
}

$c{ldap_is_master} = getLocalConfig("ldap_is_master");

if ($c{zimbra_server_hostname} eq $newServerName) {
  print "Hostname is already $newServerName.\n";
  exit 0 unless $force;
}

if (!$force) {
  my $dns=1;
  my $res = Net::DNS::Resolver->new;
  my $answer = $res->search($newServerName);
  if (!defined($answer)) {
    print "Warning: Hostname $newServerName is not resolvable via DNS\n";
    $dns=0;
  }
  if (!$dns) {
    my @a=gethostbyname("$newServerName");
    my $iaddr=splice(@a,4);
    if (!defined($iaddr)) {
      print "Error: Hostname $newServerName is not resolvable via /etc/hosts or DNS.\n";
      exit 0;
    }
  }
}

$c{zimbra_ldap_userdn} = getLocalConfig("zimbra_ldap_userdn");
$c{zimbra_ldap_password} = getLocalConfig("zimbra_ldap_password");
$c{ldap_is_master} = getLocalConfig("ldap_is_master");
$c{ldap_master_url} = getLocalConfig("ldap_master_url");
$c{ldap_starttls_supported} = getLocalConfig("ldap_starttls_supported");
$c{zimbra_log_host} = getLdapGlobalConfig("zimbraLogHostname");

# ***** Main *****

if (!$usersonly) {
  print "Renaming $c{zimbra_server_hostname} to $newServerName\n";
  print "Shutting down zimbra...";
  my $rc = runCommand("$c{zimbra_home}/bin/zmcontrol stop");
  print (($rc==0) ? "done.\n" : "failed.\n");


  updateLocalConfig("ldap_master_url");
  updateLocalConfig("ldap_url");
  updateLocalConfig("ldap_host");
  updateLocalConfig("av_notify_user");
  updateLocalConfig("av_notify_domain");
  updateLocalConfig("snmp_trap_host");
  updateLocalConfig("smtp_source");
  updateLocalConfig("smtp_destination");
  updateLocalConfig("zimbra_server_hostname");

  if (lc($c{ldap_is_master}) eq "true") {
    print "Starting ldap...";
    $rc = runCommand("$c{zimbra_home}/bin/ldap start");
    if ($rc==0) {
      print "done.\n";
    } else {
      print "failed.\n";
      exit 1;
    }
  }

  $c{zimbraServiceEnabled} = getLdapServerConfig("zimbraServiceEnabled");

  modifyServerName($newServerName);

  if (!$skipusers) {
    modifyAccountData($newServerName);
  }

  print "Services: $c{zimbraServerEnabled}\n";
  # Reinitialize mta config
  if (index($c{zimbraServiceEnabled}, "mta") != -1) {
    $c{ldap_host} = getLocalConfig("ldap_host");
    $c{ldap_port} = getLocalConfig("ldap_port");
    print "Reinitializing the mta config...";
    $rc = runCommand("$c{zimbra_home}/libexec/zmmtainit $c{ldap_host} $c{ldap_port}");
    print (($rc==0) ? "done.\n" : "failed.\n");
  }

  # Reinitialize swatchrc
  if (index($c{zimbraServiceEnabled}, "snmp") != -1) {
    print "Reinitializing the snmp config...";
    $rc = runCommand("$c{zimbra_home}/libexec/zmsnmpinit");
    print (($rc==0) ? "done.\n" : "failed.\n");
  }

  #remap or delete logger data for the host
  if (index($c{zimbraServiceEnabled}, "logger") != -1) {
    if ($delete) {
      runCommand("$c{zimbra_home}/bin/zmloggerhostmap -d $oldServerName $oldServerName");
    } else {
      runCommand("$c{zimbra_home}/bin/zmloggerhostmap $oldServerName $newServerName");
    }
  } else {
    if ($delete) {
      runCommand("$c{zimbra_home}/libexec/zmrc $c{zimbra_log_host} HOST:$c{zimbra_log_host} zmloggerhostmap -d $oldServerName $oldServerName");
    } else {
      runCommand("$c{zimbra_home}/libexec/zmrc $c{zimbra_log_host} HOST:$c{zimbra_log_host} zmloggerhostmap $oldServerName $newServerName");
    }
  }

  # Regenerate Self-signed certs
  if (!-f "$c{zimbra_home}/ssl/zimbra/commercial/commercial.crt") {
    if($c{ldap_is_master}) {
      runCommand("sudo $c{zmcertmgr} createca -new");
    }
    runCommand("sudo $c{zmcertmgr} createcrt self -new");
  }
} else {
  modifyAccountData($newServerName);
}


exit 0;
# ***** End Main *****

# ***** Subroutines *****
sub usage {
  print "\n";
  print "Usage: $0 [-h] [-d] [-f] [-s] [-o <prev servername>] [-v+] -n <servername>\n";
  print "Changes the name of the local zimbra server.\n";
  print " --help: print this usage statement.\n";
  print " --force: Force the rename, bypassing safety checks.\n";
  print " --oldServerName <oldname>: Previous name for the server.  Defaults to LC zimbra_server_hostname.\n";
  print " --newServerName <newname>: New name for the server.\n";
  print " --deletelogger: Delete the logger database for the old server.  Default is to remap its data to the new hostname.\n";
  print " --skipusers: Skips modifying the user database with the new server.\n"; 
  print " --usersonly: Only updates the user database.  This way, you can run once to do all the server\n";
  print "              updates, and then a second time to update the accounts.  Likely requires --force.\n";
  print " --verbose: Set the verbosity level.  Can be specified multiple times to increase the level.\n";
  print "\n";
  exit 0; 
}

sub modifyServerName($) {
  my ($server) = @_;
  my $config_base = "cn=config,cn=zimbra";
  my $server_base = "cn=servers,cn=zimbra";
  my $cos_base = "cn=cos,cn=zimbra";
  my $zimbra_base = "cn=zimbra";
  my $root_base = "";
  my $ldap;
  unless($ldap = Net::LDAP->new($c{ldap_master_url})) {
    print ("Unable to contact $c{ldap_master_url}: $!\n");
    return 1;
  }
  my $result;
  if ($c{ldap_master_url} !~ /^ldaps/i) {
    if ($c{ldap_starttls_supported}) {
      $result = $ldap->start_tls(
        verify => 'none',
        capath => "$c{zimbra_home}/conf/ca",
        ) or die "start_tls: $@";
      $result->code && die "TLS: " . $result->error . "\n";
    }
  }
  $result = $ldap->bind($c{zimbra_ldap_userdn}, 
    password => $c{zimbra_ldap_password});
  if ($result->code()) {
    print("ldap bind failed for $c{zimbra_ldap_userdn}\n");
    return 1;
  } else {
    print("ldap bind done for $c{zimbra_ldap_userdn}\n") if ($verbose > 1);
    print "Searching for ldap server entry...";
    $result = $ldap->search(base => $server_base, 
      scope => 'one', 
      filter => "(cn=$c{zimbra_server_hostname})");
    print ($result->code() ? "failed.\n" : "done.\n");
    return $result if ($result->code());

    # Rename server entry
    foreach my $entry ($result->all_entries) {
      print "Renaming ", $entry->dn(), "...";
      $result = $ldap->moddn(
        $entry->dn,
        newrdn=>"cn=$server",
        deleteoldrdn => 1
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraServiceHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $server_base,
      scope =>'one',
      attrs => ['1.1'],
      filter => "(&(objectClass=zimbraServer)(zimbraServiceHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraServiceHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraServiceHostname => "$server"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraMtaAuthHost attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $server_base,
      scope =>'one',
      attrs => ['cn'],
      filter => "(&(objectClass=zimbraServer)(zimbraMtaAuthHost=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraMtaAuthHost for ", $entry->dn(), "...";
      addLdapServerConfig($entry->get_value("cn"), "zimbraMtaAuthHost", "$server");
      removeLdapServerConfig($entry->get_value("cn"), "zimbraMtaAuthHost","$c{zimbra_server_hostname}");
    }
    $result = $ldap->search(base => $config_base,
      scope =>'base',
      attrs => ['1.1'],
      filter => "(&(objectClass=zimbraGlobalConfig)(zimbraMtaAuthHost=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraMtaAuthHost for ", $entry->dn(), "...";
      addLdapGlobalConfig("zimbraMtaAuthHost", "$server");
      removeGlobalConfig("zimbraMtaAuthHost","$c{zimbra_server_hostname}");
    }
    print("Updating zimbraConvertdURL attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $zimbra_base,
      scope =>'sub',
      attrs => ['zimbraConvertdURL'],
      filter => "(&(|(objectClass=zimbraServer)(objectClass=zimbraGlobalConfig))(zimbraConvertdURL=*://$c{zimbra_server_hostname}:*))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraConvertdURL for ", $entry->dn(), "...";
      my $value=$entry->get_value("zimbraConvertdURL");
      $value =~ s/$c{zimbra_server_hostname}/$server/g;
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraConvertdURL => "$value"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraSpellCheckURL attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $zimbra_base,
      scope =>'sub',
      attrs => ['zimbraSpellCheckURL'],
      filter => "(&(|(objectClass=zimbraServer)(objectClass=zimbraGlobalConfig))(zimbraSpellCheckURL=*://$c{zimbra_server_hostname}:*))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraSpellCheckURL for ", $entry->dn(), "...";
      my @value=$entry->get_value("zimbraSpellCheckURL");
      foreach my $val (@value) {
        if ($val =~ /$c{zimbra_server_hostname}/) {
          my $new_val= $val;
          $new_val =~ s/$c{zimbra_server_hostname}/$server/g;
          $result = $ldap->modify(
            $entry->dn,
            add => {zimbraSpellCheckURL => "$new_val"},
            delete => {zimbraSpellCheckURL => "$val"},
            );
          print ($result->code() ? "failed.\n" : "done.\n");
        }
      }
    }
    print("Updating zimbraSmtpHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $root_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(|(objectClass=zimbraServer)(objectClass=zimbraGlobalConfig)(objectClass=zimbraDomain))(zimbraSmtpHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraSmtpHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        add => {zimbraSmtpHostname => "$server"},
        delete => {zimbraSmtpHostname => "$c{zimbra_server_hostname}"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraWebClientAdminReference attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $root_base,
      scope =>'sub',
      attrs => ['zimbraWebClientAdminReference'],
      filter => "(&(|(objectClass=zimbraDomain)(objectClass=zimbraGlobalConfig))(zimbraWebClientAdminReference=*://$c{zimbra_server_hostname}:*))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraWebClientAdminReference for ", $entry->dn(), "...";
      my $value=$entry->get_value("zimbraWebClientAdminReference");
      $value =~ s/$c{zimbra_server_hostname}/$server/g;
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraWebClientAdminReference => "$value"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraVirtualHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $root_base,
      scope =>'sub',
      attrs => ['zimbraVirtualHostname'],
      filter => "(&(objectClass=zimbraDomain)(zimbraVirtualHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraVirtualHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        add => {zimbraVirtualHostname => "$server"},
        delete => {zimbraVirtualHostname => "$c{zimbra_server_hostname}"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraDNSCheckHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $root_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(|(objectClass=zimbraDomain)(objectClass=zimbraGlobalConfig))(zimbraDNSCheckHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraDNSCheckHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraDNSCheckHostname => "$server"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraDataSourceHost attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $root_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(objectClass=zimbraDataSource)(zimbraDataSourceHost=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraDataSourceHost for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraDataSourceHost => "$server"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraMtaMyHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $zimbra_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(|(objectClass=zimbraServer)(objectClass=zimbraGlobalConfig))(zimbraMtaMyHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraMtaMyHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraMtaMyHostname => "$server"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraMtaRelayHost attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $zimbra_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(|(objectClass=zimbraServer)(objectClass=zimbraGlobalConfig))(zimbraMtaRelayHost=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraMtaRelayHost for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        add => {zimbraMtaRelayHost => "$server"},
        delete => {zimbraMtaRelayHost => "$c{zimbra_server_hostname}"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraLogHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $config_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(objectClass=zimbraGlobalConfig)(zimbraLogHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraLogHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        add => {zimbraLogHostname => "$server"},
        delete => {zimbraLogHostname => "$c{zimbra_server_hostname}"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraPublicServiceHostname attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $root_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(|(objectClass=zimbraDomain)(objectClass=zimbraGlobalConfig))(zimbraPublicServiceHostname=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraPublicServiceHostname for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraPublicServiceHostname => "$server"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraServerHostPool attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $cos_base,
      scope =>'one',
      attrs => ['1.1'],
      filter => "(&(objectClass=zimbraCOS)(zimbraServerHostPool=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraServerHostPool for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        add => {zimbraServerHostPool => "$server"},
        delete => {zimbraServerHostPool => "$c{zimbra_server_hostname}"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
  }
  $result = $ldap->unbind;
  return 0;
}

sub modifyAccountData($) {
  my ($server) = @_;
  my $ldap_base = "";
  my $ldap;
  unless($ldap = Net::LDAP->new($c{ldap_master_url})) {
    print ("Unable to contact $c{ldap_master_url}: $!\n");
    return 1;
  }
  my $result;
  if ($c{ldap_master_url} !~ /^ldaps/i) {
    if ($c{ldap_starttls_supported}) {
      $result = $ldap->start_tls(
        verify => 'none',
        capath => "$c{zimbra_home}/conf/ca",
        ) or die "start_tls: $@";
      $result->code && die "TLS: " . $result->error . "\n";
    }
  }
  $result = $ldap->bind($c{zimbra_ldap_userdn}, 
    password => $c{zimbra_ldap_password});
  if ($result->code()) {
    print("ldap bind failed for $c{zimbra_ldap_userdn}");
    return 1;
  } else {
    print("ldap bind done for $c{zimbra_ldap_userdn}\n") if ($verbose > 1);
    print("Updating zimbraMailHost attributes\n") if ($verbose > 1);
    $result = $ldap->search(base => $ldap_base,
      scope =>'sub',
      attrs => ['1.1'],
      filter => "(&(objectClass=zimbraAccount)(zimbraMailHost=$c{zimbra_server_hostname}))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraMailHost for ", $entry->dn(), "...";
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraMailHost => "$server"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
    print("Updating zimbraMailTransport attribute\n") if ($verbose > 1);
    $result = $ldap->search(base => $ldap_base,
      scope =>'sub',
      attrs => ['zimbraMailTransport'],
      filter => "(&(objectClass=zimbraAccount)(zimbraMailTransport=*:$c{zimbra_server_hostname}:*))");
    return $result if ($result->code());
    foreach my $entry ($result->all_entries) {
      print "Updating zimbraMailTransport for ", $entry->dn(), "...";
      my $value=$entry->get_value("zimbraMailTransport");
      $value =~ s/$c{zimbra_server_hostname}/$server/g;
      $result = $ldap->modify(
        $entry->dn,
        replace => {zimbraMailTransport => "$value"},
        );
      print ($result->code() ? "failed.\n" : "done.\n");
    }
  }
  $result = $ldap->unbind;
  return 0;
}

sub updateLocalConfig($) {
  my ($key) = @_;
  my $tmpval = getLocalConfig($key);
  my $newval = $tmpval;
  $newval =~ s/$c{zimbra_server_hostname}/$newServerName/g;
  return if ($tmpval eq $newval);
  my $rc = setLocalConfig($key, $newval) ;
  return $tmpval if ($rc==0);
}

sub getLocalConfig {
  my ($key,$force) = @_;

  return $loaded{lc}{$key}
    if (exists $loaded{lc}{$key} && !$force);
  print "Getting local config $key=";
  my $val = `$c{zmlocalconfig} -x -s -m nokey ${key} 2> /dev/null`;
  chomp $val;
  $loaded{lc}{$key} = $val;
  print "$val\n"; 
  return $val;
}

sub setLocalConfig {
  my $key = shift;
  my $val = shift;

  if (exists $saved{lc}{$key} && $saved{lc}{$key} eq $val) {
    return;
  }
  $saved{lc}{$key} = $val;
  $loaded{lc}{$key} = $val;
  print "Setting local config $key=$val\n";
  runCommand("$c{zmlocalconfig} -f -e ${key}=\'${val}\' 2> /dev/null");
}

sub setLdapServerConfig($$$) {
  my ($hn,$key,$val) = @_;
  return 
    if (exists $saved{gs}{$key} && $saved{gs}{$key} eq $val);
  if ($val eq "") {
      $val="\'\'";
  }
  $loaded{gs}{$key} = $val;
  $saved{gs}{$key} = $val;
  print "Setting server config $key=$val\n" if ($verbose > 0);
  runCommand("$c{zmprov} ms $hn $key $val");
}

sub removeLdapServerConfig($$$) {
  my ($hn,$key,$val) = @_;
  print "Removing server config $key=$val\n" if ($verbose > 0);
  runCommand("$c{zmprov} ms $hn -$key $val");
}

sub addLdapServerConfig($$$) {
  my ($hn,$key,$val) = @_;
  print "Adding server config $key=$val\n" if ($verbose > 0);
  runCommand("$c{zmprov} ms $hn +$key $val");
}

sub getLdapServerConfig($$) {
  my ($key,$hn) = @_;
  $hn = $c{zimbra_server_hostname} if ($hn eq ""); 
  my $val;
  return $loaded{gs}{$hn}{$key}
    if (exists $loaded{gs}{$hn}{$key});

  print "$loaded{gs}{$hn}{$key}\n";

  print "Running $c{zmprov} gs $hn\n" if ($verbose > 1);
  unless (open(ZMPROV, "$c{zmprov} gs $hn 2> /dev/null|")) {
    print "Failed to get server config for $hn.\n";
    return;
  }
  my @CONFIG = <ZMPROV>;
  unless (close(ZMPROV)) {
    print "Failed to get server config for $hn. $@\n";
    return;
  }

  while (scalar(@CONFIG) > 0)  {
    chomp(my $line = shift(@CONFIG));
    my ($k, $v) = $line =~ m/^(\w+):\s(.*)/;
    while ($CONFIG[0] !~ m/^\w+:\s.*/ && scalar(@CONFIG) > 0) {
      chomp($v .= shift(@CONFIG));
    }

    next if ($k =~ m/cn|objectClass/);
    $loaded{gs}{$hn}{$k} .= 
      (($loaded{gs}{$hn}{$k} eq "") ? "$v" : " $v");;
  }

  return $loaded{gs}{$hn}{$key};
}

sub setLdapGlobalConfig($$) {
  my ($key,$val) = @_;
  return 
    if (exists $saved{gcf}{$key} && $saved{gcf}{$key} eq $val);
  if ($val eq "") {
      $val="\'\'";
  }
  $loaded{gcf}{$key} = $val;
  $saved{gcf}{$key} = $val;
  print "Setting global config $key=$val\n" if ($verbose > 0);
  runCommand("$c{zmprov} mcf $key $val");
}

sub removeLdapGlobalConfig($$) {
  my ($key,$val) = @_;
  print "Removing global config $key=$val\n" if ($verbose > 0);
  runCommand("$c{zmprov} mcf -$key $val");
}

sub addLdapGlobalConfig($$) {
  my ($key,$val) = @_;
  print "Adding global config $key=$val\n" if ($verbose > 0);
  runCommand("$c{zmprov} mcf +$key $val");
}

sub getLdapGlobalConfig($) {
  my ($key) = @_;
  my ($val); 
  return $loaded{gcf}{$key}
    if (exists $loaded{gcf}{$key});

  print "Getting global config $key=";
  open(ZMPROV, "$c{zmprov} gcf $key|");
  my @CONFIG = <ZMPROV>;
  close(ZMPROV);

  while (scalar(@CONFIG) > 0)  {
    chomp(my $line = shift(@CONFIG));
    my ($k, $v) = $line =~ m/^(\w+):\s(.*)/;
    while ($CONFIG[0] !~ m/^\w+:\s.*/ && scalar(@CONFIG) > 0) {
      chomp($v .= shift(@CONFIG));
    }

    next if ($k =~ m/cn|objectClass/);
    $val .= (($val eq "") ? "$v" : " $v") if ($k eq $key);
    $loaded{gcf}{$k} .= 
      (($loaded{gcf}{$k} eq "") ? "$v" : " $v");;
  }
  print "$loaded{gcf}{$key}\n";
  return $loaded{gcf}{$key};
}

sub runCommand {
  my $cmd = shift;
  my $rc;
  $rc = 0xffff & system("$cmd > /dev/null 2>&1 ");
  return $rc;
}

